               #org 0xc000
; **********************************************
; *****                                    *****
; *****    MINIMAL BLOCKS by slu4 (2021)   *****
; *****                                    *****
; *****       last update 03.06.2021       *****
; *****            ROM Version             *****
; *****                                    *****
; **********************************************
              LDI 0xfe STA 0xffff         ; init stack
game_restart: CLB state
              JPS PrintIntro

game_loop:    LDI 27 OUT JPS _WaitUART    ; hide cursor
              LDI '[' OUT JPS _WaitUART
              LDI 'H' OUT JPS _WaitUART
              JPS Sleep                   ; refresh 1/60s
              JPS Randomize
              LDA state DEC               ; switch state
              BCC state_intro
                DEC
                BCC state_run
                  JPA state_over


; ***** STATE INTRO **********************************************************************************

state_intro:  INP CPI 32               ; non-blocking key input
              BNE game_loop            ; immediate loop-back
                LDI <vram STA ptr+0
                LDI >vram STA ptr+1    ; clear playfield
  cfloop:       LDI '.' STR ptr
                INW ptr
                LDA ptr+0
                CPI 200
                BCC cfloop                
                  CLW score            ; reset all game variables
                  CLW gameframes
                  LDI 80 STA waiting   ; fall timer init
                  CLB dropsteps
                  JPS PrintFrame       ; print empty field
                  JPS NewShape         ; pick random shape to start with (no test required)
                  LDI '#' STA shapechar JPS PrintShape       ; draw tetromino
                  LDI <scoretext PHS
                  LDI >scoretext PHS
                  JPS _Print PLS PLS
                  LDI <hightext PHS
                  LDI >hightext PHS
                  JPS _Print PLS PLS
                  LDA highscore+0 PHS LDA highscore+1 PHS
                  JPS U16_Print PLS PLS
                  LDI <controltext PHS
                  LDI >controltext PHS
                  JPS _Print PLS PLS
                  INB state            ; STATE = RUNNING!!!
                  JPA game_loop

state_run:    INW gameframes           ; count time
              DEB waiting              ; tetromino falling?
              BPL run_keyb
                LDA gameframes+1       ; set fall timer 'waiting'
                LSR NEG ADI 80 STA waiting
                LDA xpos PHS LDA ypos INC PHS
                JPS TestShape PLS
                PLS DEC BCC run_place                   ; no space? place it here
                  LDI '.' STA shapechar JPS PrintShape  ; free space? let it fall
                  INB ypos             
                  LDI '#' STA shapechar JPS PrintShape

  run_keyb:   INP BEQ game_loop                         ; non-blocking key input
              CPI 'a' BEQ a_key
              CPI 'd' BEQ d_key
              CPI 'w' BEQ w_key
              CPI 's' BEQ s_key
              JPA game_loop

  a_key:      LDI '.' STA shapechar JPS PrintShape
              LDA xpos DEC PHS LDA ypos PHS JPS TestShape PLS
              PLS SBB xpos
              JPA r_drshape
  d_key:      LDI '.' STA shapechar JPS PrintShape
              LDA xpos INC PHS LDA ypos PHS JPS TestShape PLS
              PLS ADB xpos
              JPA r_drshape
  w_key:      LDI '.' STA shapechar JPS PrintShape  ; delete old shape
              JPS RotateShape                       ; make rotate shape
              LDA xpos PHS LDA ypos PHS JPS TestShape PLS PLS DEC BCS r_drshape                  ; test position
              LDA xpos INC STA xpos PHS LDA ypos PHS JPS TestShape PLS PLS DEC BCS r_drshape     ; and possible wall kicks
              LDA xpos SBI 2 STA xpos PHS LDA ypos PHS JPS TestShape PLS PLS DEC BCS r_drshape
              LDA xpos ADI 3 STA xpos PHS LDA ypos PHS JPS TestShape PLS PLS DEC BCS r_drshape
              LDA xpos SBI 4 STA xpos PHS LDA ypos PHS JPS TestShape PLS PLS DEC BCS r_drshape
                INB xpos           ; back to beginning
                INB xpos
                JPS RotateShape    ; rotation wasn't free -> rotate back
                JPS RotateShape
                JPS RotateShape
                JPA r_drshape
  s_key:      JPS Randomize
              INB dropsteps
              LDI -1 STA waiting
              JPA game_loop

  r_drshape:  LDI '#' STA shapechar JPS PrintShape
              JPA game_loop

  run_place:  LDI <shape STA ptr+0
              LDI >shape STA ptr+1
              LDI >vram STA ptr2+1
              LDI 4 STA vari
  rploop:     LDA xpos                ; DRAW SHAPE TO VRAM
              ADR ptr                 ; add shape xoffset
              STA ptr2+0              ; write x info
              INW ptr                 ; goto shape yoffset
              LDA ypos
              ADR ptr                 ; add shape yoffset
              LSL PHS ADW ptr2        ; add y x 2 zum vram-pointer
              PLS LSL LSL ADW ptr2
              LDI '#' STR ptr2        ; write to VRAM
              INW ptr
              DEB vari
              BNE rploop
                CLB anzrows
                LDI 10 STA ptr2+0     ; start of VRAM = Anfang Reihe 1
                LDI >vram STA ptr+1
                STA ptr1+1
                LDI 19 STA vary       ; test 19 rows
  rpyloop:    LDI 1 STA rowfull       ; assume row is full
              LDI 10 STA varx
  rpxloop:    LDI '.' CPR ptr2
              BNE rpiswall 
                CLB rowfull           ; mark row as not empty
  rpiswall:   INB ptr2
              DEB varx
              BNE rpxloop
                LDA rowfull DEC
                BCC rpcopydone
                  INB anzrows         ; row is full
                  LDA ptr2+0 DEC STA ptr+0
                  SBI 10 STA ptr1+0
  rpcopyloop:     LDR ptr1 STR ptr
                  DEB ptr DEB ptr1
                  BCS rpcopyloop
  rpleerloop:       INB ptr1
                    CPI 10
                    BCC rpcopydone
                    LDI '.' STR ptr1
                    JPA rpleerloop
  rpcopydone:   DEB vary
                BNE rpyloop
                  LDI >wintable STA ptr+1 LDI <wintable STA ptr+0
                  LDA anzrows ADW ptr
                  LDR ptr ADW score LDR ptr ADW score  ; add score for cleared rows
                  LDA dropsteps ADW score              ; add the drop points
                  LDI <pretext PHS LDI >pretext PHS
                  JPS _Print PLS PLS
                  LDA score+0 PHS LDA score+1 PHS
                  JPS U16_Print PLS PLS ; print score
                  LDA score+1           ; check if there is a new highscore
                  CPA highscore+1
                  BCC rpnewshape
                  BNE rpnewhigh  
                    LDA score+0
                    CPA highscore+0
                    BCC rpnewshape
  rpnewhigh:          LDA score+0 STA highscore+0   ; score = highscore
                      LDA score+1 STA highscore+1

  rpnewshape:   CLB dropsteps
                JPS NewShape            ; pick random shape
                JPS PrintField          ; draw field
                JPS PrintShape          ; Tetromino malen
                LDA xpos PHS LDA ypos PHS JPS TestShape PLS
                PLS DEC BCS game_loop
                  INB state             ; INVALID POSITION => GAME OVER
                  JPS PrintOver
                  JPA game_loop

state_over:   INP CPI 32
              BNE game_loop
                JPA game_restart

NewShape:     LDI <shape STA ptr2+0  ; copy a random piece into shape
              LDI >shape STA ptr2+1
              LDI <minos STA ptr+0
              LDI >minos STA ptr+1
              LDA randomnum LSL LSL LSL ADA randomnum   ; x 9
              ADW ptr
              LDI 8 STA vari
  nscopyloop: LDR ptr STR ptr2
              INW ptr INW ptr2
              DEB vari
              BNE nscopyloop
                LDI 4 STA xpos
                LDI 1 STA ypos
                CLB yoff
                LDR ptr STA xoff                
                RTS

TestShape:    LDI <shape STA ptr+0    ; test if pos is free
              LDI >shape STA ptr+1
              LDI >vram STA ptr2+1
              LDI 4 STA vari
  tsloop:     LDS 4                   ; lade test-xpos
              ADR ptr                 ; addiere shape-xoffset hinzu
              BMI tsoutside           ; pr端fe linke Grenze
                CPI 10
                BCS tsoutside         ; pr端fe rechte Grenze     
              STA ptr2+0              ; beschreibe vram-pointer mit x-info
              INW ptr                 ; gehe zum shape-yoffset
              LDS 3                   ; lade test-ypos
              ADR ptr                 ; addiere shape-yoffset hinzu
              BMI tsoutside           ; pr端fe obere Grenze
                CPI 20
                BCS tsoutside         ; pr端fe untere Grenze            
              LSL PHS ADW ptr2        ; addiere y x 2 zum vram-pointer
              PLS LSL LSL ADW ptr2    ; addiere y x 8 zum vram-pointer
              LDR ptr2                ; lies VRAM an dieser Stelle
              CPI '#'
              BEQ tsoutside
                INW ptr
                DEB vari
                BNE tsloop
                  LDI 1 STS 4 ; return 'space is valid'
                  RTS
  tsoutside:  LDI 0 STS 4     ; return 'space is invalid'
              RTS

RotateShape:  LDA xoff ADB xpos
              LDA yoff ADB ypos
              LDI 5 STA vari
              LDI >shape STA ptr+1
              LDI <shape STA ptr+0
  rsloop:     LDR ptr PHS INW ptr
              LDR ptr PHS DEW ptr
              PLS STR ptr INW ptr
              PLS NEG STR ptr INW ptr
              DEB vari
              BNE rsloop
                RTS

PrintShape:   LDI >shape STA ptr+1
              LDI <shape STA ptr+0
              LDI 4 STA vari
  psloop:     LDA xpos ADI 15 ADR ptr
              PHS JPS _CursorX PLS
              INW ptr
              LDA ypos INC ADR ptr
              PHS JPS _CursorY PLS
              LDA shapechar OUT JPS _WaitUART
              INW ptr         ; next square
              DEB vari
              BNE psloop
                RTS

PrintField:   LDI 27 OUT JPS _WaitUART 
              LDI '[' OUT JPS _WaitUART
              LDI 'H' OUT JPS _WaitUART
              LDI >vram STA ptr+1
              CLB ptr+0
              LDI 20 STA vary
  pflinstart: LDI 15 PHS JPS _CursorX PLS
              LDI 10 STA varx
  pfxloop:    LDR ptr 
              OUT JPS _WaitUART
              INB ptr+0
              DEB varx
              BNE pfxloop
                LDI 10 OUT JPS _WaitUART
                DEB vary
                BNE pflinstart
                  RTS              

PrintFrame:   LDI <clrtext PHS LDI >clrtext PHS
              JPS _Print PLS PLS              
              LDI 20 STA vary              
  pfrloop:    LDI <textframe1 PHS
					    LDI >textframe1 PHS
					    JPS _Print PLS PLS
              DEB vary
              BNE pfrloop
                LDI <textframe2 PHS
                LDI >textframe2 PHS
                JPS _Print PLS PLS
                LDI <textframe3 PHS
                LDI >textframe3 PHS
                JPS _Print PLS PLS
                RTS

PrintIntro:   LDI <tetristext PHS
					    LDI >tetristext PHS
					    JPS _Print PLS PLS
              JPA PrintSpace

PrintOver:    LDI <overtext PHS
					    LDI >overtext PHS
					    JPS _Print PLS PLS

PrintSpace:   LDI <spacetext PHS
					    LDI >spacetext PHS
					    JPS _Print PLS PLS
              RTS 

Randomize:    INB randomnum     ; Randomize with time
              CPI 7
              BCC randrts
                CLB randomnum   ; overflow
  randrts:    RTS

Sleep:        CLB ptr+0 LDI 0x04 STA ptr+1
  sloop:      DEW ptr
              BCS sloop
                RTS

; print out an unsigned 16-bit decimal number in the format 00000
; push: number_lsb, number_msb
; pull: #, #
U16_Print:		  LDS 3 STA U16_C+1	; PRINT A POSITIVE NUMBER
                LDS 4 STA U16_C+0
                LDI 0 PHS STA U16_digits
	U16_start:		CLB U16_C+2
                LDI 16 STA U16_count
	U16_shift:		LDA U16_C+2 ROL
								LDA U16_C+0 ROL STA U16_C+0
								LDA U16_C+1 ROL STA U16_C+1
								LDA U16_C+2 ROL STA U16_C+2
								CPI 10 BCC U16_done
									ADI 118 STA U16_C+2
	U16_done:		  DEB U16_count BNE U16_shift
									LDA U16_C+2 LSL LSR
                  ADI '0' PHS INB U16_digits
									LDA U16_C+2 ROL
									LDA U16_C+0 ROL STA U16_C+0
									LDA U16_C+1 ROL STA U16_C+1
									LDA U16_C+2 ROL STA U16_C+2
                  LDI 0
									CPA U16_C+0 BNE U16_start
									  CPA U16_C+1 BNE U16_start
  U16_before:         INB U16_digits CPI 6 BEQ U16_stack
                        LDI '0' OUT JPS _WaitUART JPA U16_before
  U16_stack:			    PLS CPI 0 BEQ U16_exit
										    OUT JPS _WaitUART
										    JPA U16_stack
	U16_exit:			      RTS

textframe1:   27, '[12C<!..........!>', 10, 0
textframe2:   27, '[12C<!==========!>', 10, 0
textframe3:   27, '[12C  VVVVVVVVVV  ', 10, 0

tetristext:   27, '[H', 27, '[J', 27
              '[10B M I N I M A L   U A R T   B L O C K S', 10, 0
overtext:     27, '[H', 27, '[10B', 27, '[14CGAME  OVER', 10, 0
spacetext:    27, '[12B', 27, '[13C', 'Press  SPACE', 10, 0
scoretext:    27, '[HSCORE 00000', 0
hightext:     27, '[16CHIGH ', 0
pretext:      27, '[6C', 0
clrtext:      27, '[H', 27, '[J', 0
controltext:  10, 10, 10                     ; output text for keyboard controls
              'CONTROLS', 10, 10
              'A - Left', 10
              'D - Right', 10
              'W - Rotate', 10
              'S - Drop', 0

              ;  initial tetrominos with initial x-offset for SRS
minos:        0,  0,  1,  0,  0, -1, 1, -1,   1  ; square
              -1, -1, 0,  -1, 0, 0,  1, 0,    0  ; Z
              -1, 0,  0,  0,  0, -1, 1, -1,   0  ; neg. Z
              -1, 0,  0,  0,  1, 0,  2, 0,    1  ; slab
              -1, 0,  0,  0,  1, 0,  0, -1,   0  ; pyramid
              -1, -1, -1, 0,  0, 0,  1, 0,    0  ; L
              -1, 0,  0,  0,  1, 0,  1, -1,   0  ; neg. L

wintable:     0, 20, 50, 100, 250     ; points depending on cleared rows (x2)

highscore:    0x1bca                  ; holding the current highscore 10.5.2021

#end

#org 0xef00   vram:                   ; 20*10 bytes Video RAM

#org 0xefc8   ; variables

U16_C:        0xffff, 0xff            ; for U16_Print
U16_count:    0xff
U16_digits:   0xff

shapechar:    0                       ; current look of a piece, either '#' or '.'
shape:        0, 0, 0, 0, 0, 0, 0, 0  ; current shape (and it's rotation state)
xoff:         0                       ; SRS rotation compensation
yoff:         0

state:        0          ; 0: intro, 1: running, 2: over
score:        0x0000     ; holding the current score 
waiting:      0          ; timer 
randomnum:    0          ; Random number generator
gameframes:   0x0000     ; counting the game frames
xpos:         0          ; current position of tetromino
ypos:         0    
ptr:          0x0000     ; multi-purpose pointers
ptr1:         0x0000
ptr2:         0x0000
vari:         0          ; multi-purpose
varx:         0
vary:         0
dropsteps:    0          ; counting hard drops
anzrows:      0          ; count cleared lines
rowfull:      0          ; boolean line completed

#end          ; use these OS API functions

#org 0xf006   _Print:    ; Prints a null-terminated string
#org 0xf00c   _WaitUART: ; Waits for UART transmission
#org 0xf022   _CursorX:  ; set the cursor x position 1..60
#org 0xf025   _CursorY:  ; set the cursor y position 1..25
